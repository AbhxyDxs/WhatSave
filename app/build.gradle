import org.jetbrains.annotations.Nullable

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'com.google.devtools.ksp'
}

def getProperties(String fileName) {
    Properties properties = new Properties()
    def file = rootProject.file(fileName)
    if (file.exists()) {
        file.withInputStream { stream -> properties.load(stream) }
    } else {
        properties = null
    }
    return properties
}

static def getProperty(@Nullable Properties properties, String name) {
    return properties?.getProperty(name) ?: "$name missing"
}

android {
    compileSdk 33
    namespace "com.simplified.wsstatussaver"

    defaultConfig {
        minSdk 21
        targetSdk 33

        applicationId namespace
        versionCode 101
        versionName "1.0.1"
    }

    def signingProperties = getProperties('keystore.properties')
    def releaseSigning
    if (signingProperties != null) {
        releaseSigning = signingConfigs.create("release") {
            storeFile file(getProperty(signingProperties, 'storeFile'))
            keyAlias getProperty(signingProperties, 'keyAlias')
            storePassword getProperty(signingProperties, 'storePassword')
            keyPassword getProperty(signingProperties, 'keyPassword')
        }
    } else {
        releaseSigning = signingConfigs.debug
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-rules.pro')
            signingConfig releaseSigning
        }
        debug {
            signingConfig releaseSigning
            applicationIdSuffix '.debug'
            versionNameSuffix ' DEBUG'
        }
    }
    buildFeatures {
        dataBinding true
        viewBinding true
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE', 'META-INF/NOTICE', 'META-INF/java.properties']
        }
    }
    lint {
        abortOnError true
        warning 'ImpliedQuantity', 'Instantiatable', 'MissingQuantity', 'MissingTranslation'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    dependenciesInfo {
        includeInApk = false
        includeInBundle = false
    }
}

dependencies {
    // Google/JetPack
    //https://developer.android.com/jetpack/androidx/versions
    implementation "androidx.core:core-ktx:$core_version"
    implementation "androidx.core:core-splashscreen:1.0.1"
    implementation 'androidx.annotation:annotation:1.7.0-rc01'
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation('androidx.fragment:fragment-ktx:1.6.1') {
        exclude module: 'loader'
    }

    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"

    implementation 'androidx.room:room-ktx:2.5.2'
    ksp 'androidx.room:room-compiler:2.5.2'

    implementation 'androidx.loader:loader:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.1'
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.preference:preference-ktx:$preference_version"
    implementation("com.google.android.material:material:$mdc_version") {
        exclude module: 'viewpager2'
    }

    def koin_version = '3.4.0'
    implementation "io.insert-koin:koin-core:$koin_version"
    implementation "io.insert-koin:koin-android:$koin_version"

    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'com.github.bumptech.glide:glide:4.15.1'

    // Kotlin
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"
}